apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def globalConfiguration = rootProject.extensions.getByName("ext")

android {
    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

    defaultConfig {
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
        versionCode globalConfiguration.getAt("androidVersionCodeMainLibrary")
        versionName globalConfiguration.getAt("androidVersionNameMainLibrary")
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    def libraryDependencies = globalConfiguration.mainLibraryDependencies
    implementation libraryDependencies.AndroidAnnotations
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = globalConfiguration.getAt("jcenterGroupId")
                artifactId = globalConfiguration.getAt("jcenterArtifactIdMainLibrary")
                version = globalConfiguration.getAt("androidVersionNameMainLibrary")

                pom {
                    name = globalConfiguration.getAt("jcenterArtifactIdMainLibrary")
                    description = globalConfiguration.getAt("jcenterDescriptionMainLibrary")
                    url = globalConfiguration.getAt("jcenterWebsite")
                }
            }
        }
        repositories {
            mavenLocal()
        }
    }
}

tasks.named('publishToMavenLocal') {
    dependsOn 'publishReleasePublicationToMavenLocal'
}
